My Solutions for "Ninety-Nine Haskell Problems"
===============================================

The problems are listed [here](http://www.haskell.org/haskellwiki/H-99:_Ninety-Nine_Haskell_Problems).

Problem 1
---------

### Description

Find the last element of a list.

Example in Haskell:

    Prelude> myLast [1,2,3,4]
    4
    Prelude> myLast ['x','y','z']
    'z'

### Solutions

```haskell
myLast :: [a] -> a
myLast [x] = x
myLast (_:xs) = myLast xs
    
myLast' :: [a] -> a
myLast' = head . reverse
    
myLast'' :: [a] -> a
myLast'' = foldl1 (flip const)    
    
myLast''' :: [a] -> a
myLast''' = foldr1 (flip const)
    
-- The following are confusing and amazing. 
    
myLast'''' :: [a] -> a
myLast'''' = foldr1 (const id)
    
myLast''''' :: [a] -> a
myLast''''' = foldl1 (const id)
```
Problem 2
---------

### Description

Find the last but one element of a list.

Example in Haskell:

    Prelude> myButLast [1,2,3,4]
    3
    Prelude> myButLast ['a'..'z']
    'y'
    
### Solutions

```haskell
myButLast :: [a] -> a
myButLast x = reverse x !! 1
    
myButLast' :: [a] -> a
myButLast' [x, _] = x
myButLast' (x : xs) = myButLast' xs
    
myButLast'' :: [a] -> a
myButLast'' = last . init
    
myButLast''' :: [a] -> a
myButLast''' = head . tail . reverse
```

Problem 3
---------

### Description

Find the K'th element of a list. The first element in the list is number 1.

Example in Haskell

    Prelude> elementAt [1,2,3] 2
    2
    Prelude> elementAt "haskell" 5
    'e'

### Solutions

```haskell
elementAt :: [a] -> Int -> a
elementAt xs i = xs !! (i - 1)
    
elementAt' :: [a] -> Int -> a
elementAt' (x : _) 1 = x 
elementAt' (_ : xs) i 
  | i < 1 = error "Index out of bounds"
  | otherwise = elementAt' xs (i-1)
elementAt' [] _ = error "Index out of bounds"
    
elementAt'' :: [a] -> Int -> a
elementAt'' xs i = last . take i $ xs 
```
Problem 4
---------

### Description

Find the number of elements of a list.

Example in Haskell:

    Prelude> myLength [123, 456, 789]
    3
    Prelude> myLength "Hello, world!"
    13
    
### Solutions

```haskell
myLength :: [a] -> Int
myLength [] = 0 
myLength (_ : xs) = 1 + myLength xs
    
myLength' :: [a] -> Int
myLength' = sum . map (\x -> 1)
    
myLength'' :: [a] -> Int
myLength'' = foldl (\n _ -> n+1) 0
    
myLength''' :: [a] -> Int
myLength''' = foldr (\_ n -> n+1) 0
    
myLength'''' :: [a] -> Int
myLength'''' = foldr (const (+1)) 0
```

Problem 5
---------

### Description

Reverse a list.

Example in Haskell:

    Prelude> reverse "A man, a plan, a canal, panama!"
    "!amanap ,lanac a ,nalp a ,nam A"
    Prelude> reverse [1,2,3,4]
    [4,3,2,1]

### Solutions

```haskell
reverse' :: [a] -> [a]
reverse' [] = []
reverse' (x:xs) = reverse' xs ++ [x]
    
reverse'' :: [a] -> [a]
reverse'' = foldl (flip (:)) []
    
reverse''' :: [a] -> [a]
reverse''' list = reverse'''' list []
  where reverse'''' [] reversed = reversed
        reverse'''' (x:xs) reversed = reverse'''' xs (x:reversed)
```

Problem 6
---------

### Description

Find out whether a list is a palindrome. A palindrome can be read forward or backward; e.g. (x a m a x).

Example in Haskell:

    Prelude> isPalindrome [1,2,3]
    False
    Prelude> isPalindrome "madamimadam"
    True
    Prelude> isPalindrome [1,2,4,8,16,8,4,2,1]
    True

### Solutions

```haskell
isPalindrome :: Eq a => [a] -> Bool
isPalindrome list = list == reverse list
    
isPalindrome' :: Eq a => [a] -> Bool
isPalindrome' [] = True
isPalindrome' [_] = True
isPalindrome' list = (head list) == (last list) && (isPalindrome' $ init $ tail list)
```

Problem 7
---------

### Description

Flatten a nested list structure.

Transform a list, possibly holding lists as elements into a "flat" list by replacing each list with its elements (recursively).

Example in Haskell:

    Prelude> flatten (Elem 5)
    [5]
    Prelude> flatten (List [Elem 1, List [Elem 2, List [Elem 3, Elem 4], Elem 5]])
    [1,2,3,4,5]
    Prelude> flatten (List [])
    []

### Solutions

We should define a data structure like this:
```haskell
data NestedList a = Elem a | List [NestedList a]
```
The following are the solutions:
```haskell
flatten :: NestedList a -> [a]
flatten (Elem x) = [x]
flatten (List x) = concatMap flatten x
    
flatten' :: NestedList a -> [a]
flatten' (Elem x) = [x]
flatten' (List []) = []
flatten' (List (x:xs)) = flatten' x ++ flatten' (List xs)
    
flatten'' :: NestedList a -> [a]
flatten'' (Elem x) = [x]
flatten'' (List x) = foldr (++) [] $ map flatten'' x
```

Problem 8
---------

### Description

Eliminate consecutive duplicates of list elements.

If a list contains repeated elements they should be replaced with a single copy of the element. The order of the elements should not be changed.

Example in Haskell:

    Prelude> compress ["a","a","a","a","b","c","c","a","a","d","e","e","e","e"]
    ["a","b","c","a","d","e"]
    
### Solutions

```haskell
compress :: Eq a => [a] -> [a]
compress = foldr compHelp []
  where compHelp x [] = [x]
        compHelp x (y:ys) 
          | x == y = y:ys
          | otherwise = x:y:ys
      
compress' :: Eq a => [a] -> [a]
compress' [] = []
compress' (x:xs) = x : (compress $ dropWhile (== x) xs)
    
compress'' :: Eq a => [a] -> [a]
compress'' x = foldr (\a b -> if a == (head b) then b else a:b) [last x] x
```

If we import functions in Data.List, then we could have an easier version:

```haskell
compress''' :: Eq a => [a] -> [a]
compress''' = map head . group
```

Problem 9
---------

### Description

Pack consecutive duplicates of list elements into sublists. If a list contains repeated elements they should be placed in separate sublists.

Example in Haskell:
    
    Prelude> pack ['a', 'a', 'a', 'a', 'b', 'c', 'c', 'a', 'a', 'd', 'e', 'e', 'e', 'e']
    ["aaaa","b","cc","aa","d","eeee"]
    
### Solutions

There is a most trivial version:

```haskell
pack :: Eq a => [a] -> [[a]]
pack [] = []
pack (x:xs) = packHelper [] [] (x:xs)
  where packHelper result [] [] = result
        packHelper result current [] = result ++ [current]
        packHelper result [] (x:xs) = packHelper result [x] xs
        packHelper result current@(y:_) (x:xs) 
          | y == x = packHelper result (x:current) xs
          | otherwise = packHelper (result ++ [current]) [x] xs
```

The structure of packHelper implies me to use foldr:

```haskell
pack' :: Eq a => [a] -> [[a]]
pack' = foldr packHelper []
  where packHelper item [] = [[item]]
        packHelper item result@(x:xs)
          | item == head x = (item:x) : xs
          | otherwise = [item] : result
```

It's actually the function Data.List.group. The implementation is:

```haskell
pack'' :: Eq a => [a] -> [[a]]
pack'' [] = []
pack'' (x:xs) = let (first, rest) = span (==x) xs
                in (x:first) : pack'' rest
```

Problem 10
----------

### Description

Run-length encoding of a list. Use the result of problem P09 to implement the so-called run-length encoding data compression method. Consecutive duplicates of elements are encoded as lists (N E) where N is the number of duplicates of the element E.

Example in Haskell:
    
    Prelude> encode "aaaabccaadeeee"
    [(4,'a'),(1,'b'),(2,'c'),(2,'a'),(1,'d'),(4,'e')]
    
### Solutions

If we use Data.List.group, the solution is obvious:

```haskell
encode :: Eq a => [a] -> [(Int, a)]
encode list = let grouped = group list
              in zip (map length grouped) (map head grouped)
          
encode' :: Eq a => [a] -> [(Int, a)]
encode' = map (\x -> (length x, head x)) . group
```
Also we could have a version which uses foldr:

```haskell
encode'' :: Eq a => [a] -> [(Int, a)]
encode'' = foldr encodeHelper []
  where encodeHelper item [] = [(1, item)]
        encodeHelper item result@((count, x):xs)
          | item == x = (count + 1, x):xs
          | otherwise = (1, item) : result
```

Problem 11
----------

### Description

Modified run-length encoding.

Modify the result of problem 10 in such a way that if an element has no duplicates it is simply copied into the result list. Only elements with duplicates are transferred as (N E) lists.

Example in Haskell:

    Prelude> encodeModified "aaaabccaadeeee"
    [Multiple 4 'a',Single 'b',Multiple 2 'c',Multiple 2 'a',Single 'd',Multiple 4 'e']

### Solutions

```haskell
data Problem11 a = Single a | Multiple Int a deriving (Show)
encodeModified :: Eq a => [a] -> [Problem11 a]
encodeModified = map transformHelper . encode
  where transformHelper (1, x) = Single x
        transformHelper (n, x) = Multiple n x
```

Problem 12
----------

### Description

Decode a run-length encoded list.

Given a run-length code list generated as specified in problem 11. Construct its uncompressed version.

Example in Haskell:

    Prelude> decodeModified [Multiple 4 'a',Single 'b',Multiple 2 'c',Multiple 2 'a',Single 'd',Multiple 4 'e']
    "aaaabccaadeeee"
    
### Solutions

```haskell
decodeModified :: [Problem11 a] -> [a]
decodeModified = concatMap decodeHelper
  where decodeHelper (Single x) = [x]
        decodeHelper (Multiple n x) = replicate n x
```

Problem 13
----------

### Description

Run-length encoding of a list (direct solution).

Implement the so-called run-length encoding data compression method directly. I.e. don't explicitly create the sublists containing the duplicates, as in problem 9, but only count them. As in problem P11, simplify the result list by replacing the singleton lists (1 X) by X.

Example in Haskell:

    Prelude> encodeDirect "aaaabccaadeeee"
    [Multiple 4 'a',Single 'b',Multiple 2 'c',Multiple 2 'a',Single 'd',Multiple 4 'e']
    
### Solutions

```haskell
encodeDirect :: Eq a => [a] -> [Problem11 a]
encodeDirect = foldr encodeHelper []
  where encodeHelper x result@(h:rest) 
          | (getContent h) == x = (Multiple (1 + getCount h) x) : rest
          | otherwise = (Single x) : result
        encodeHelper x [] = [(Single x)]
        getContent (Single x) = x
        getContent (Multiple _ x) = x
        getCount (Single _) = 1
        getCount (Multiple n _) = n
```

Problem 14
----------

### Description

Duplicate the elements of a list.

Example in Haskell:

    Prelude> dupli [1, 2, 3]
    [1,1,2,2,3,3]
    
### Solutions
    
```haskell
dupli :: [a] -> [a]
dupli = concatMap (\x->[x,x])
    
dupli' :: [a] -> [a]
dupli' = foldr (\x xs -> x:x:xs) []
```

Problem 15
----------

### Description

Replicate the elements of a list a given number of times.

Example in Haskell:

    Prelude> repli "abc" 3
    "aaabbbccc"
    
### Solutions
```haskell
repli :: [a] -> Int -> [a]
repli xs n = concatMap (replicate n) xs
    
repli' :: [a] -> Int -> [a]
repli' = flip $ concatMap . replicate
```

Problem 16
----------

### Description
Drop every N'th element from a list.

Example in Haskell:

    Prelude> dropEvery "abcdefghik" 3
    "abdeghk"
    
### Solutions

At first I give a tail-recursive version:

```haskell
dropEvery :: [a] -> Int -> [a]
dropEvery [] _ = []
dropEvery _ 1 = []
dropEvery xs 0 = xs
dropEvery (x:xs) n = dropHelper [x] (n-1) xs
  where dropHelper result i [] = result
        dropHelper result 1 (_:ys) = dropHelper result n ys
        dropHelper result i (y:ys) = dropHelper (result ++ [y]) (i-1) ys
```

Then I think I could use zipWith, but I failed. I finally use zip instead:

```haskell
dropEvery' :: [a] -> Int -> [a]
dropEvery' xs 0 = xs
dropEvery' [] _ = []
dropEvery' xs n = map snd $ filter ((n /=) . fst) $ zip (cycle [1..n]) xs
```

Problem 17
----------

### Description
Split a list into two parts; the length of the first part is given.

Do not use any predefined predicates.

Example in Haskell:

    Prelude> split "abcdefghik" 3
    ("abc", "defghik")
    
### Solutions
```haskell
split :: [a] -> Int -> ([a], [a])
split xs n = splitHelper [] n xs
  where splitHelper list 0 ys = (list, ys) 
        splitHelper list i [] = (list, [])
        splitHelper list i (y:ys) = splitHelper (list ++ [y]) (i-1) ys
    
split' :: [a] -> Int -> ([a], [a])
split' xs 0 = ([], xs)
split' [] i = ([], [])
split' (x:xs) n = (x:f, l) where (f,l) = split' xs (n-1)
```

Problem 18
----------

### Description
Extract a slice from a list.

Given two indices, i and k, the slice is the list containing the elements between the i'th and k'th element of the original list (both limits included). Start counting the elements with 1.

Example in Haskell:

    Prelude> slice "abcdefghik" 3 7
    "cdefg"
    
### Solutions
```haskell
slice :: [a] -> Int -> Int -> [a]
slice xs h t = sliceHelper [] xs 1
  where sliceHelper l [] _ = l
        sliceHelper l (y:ys) i
          | i > t = l
          | i < h = sliceHelper l ys (i+1) 
          | otherwise = sliceHelper (l ++ [y]) ys (i+1)
      
slice' :: [a] -> Int -> Int -> [a]
slice' xs h t = map snd $ filter ((>=h) . fst) $ zip [1..t] xs
    
slice'' :: [a] -> Int -> Int -> [a]
slice'' [] _ _ = []
slice'' (x:xs) h t
  | h > 1 = slice'' xs (h - 1) (t - 1)
  | t < 1 = []
  | otherwise = x:slice'' xs (h - 1) (t - 1)
```

Problem 19
----------

### Description
Rotate a list N places to the left.

Examples in Haskell:

    Prelude> rotate ['a','b','c','d','e','f','g','h'] 3
    "defghabc"
    
    Prelude> rotate ['a','b','c','d','e','f','g','h'] (-2)
    "ghabcdef"
    
### Solutions
```haskell
rotate :: [a] -> Int -> [a]
rotate xs 0 = xs
rotate xs i 
  | i > len = rotate xs (mod i len)
  | i > 0 = swapConcat $ splitAt i xs
  | i < 0 = rotate xs $ length xs + i
  where swapConcat (h,t) = t ++ h
        len = length xs
        
rotate' :: [a] -> Int -> [a]
rotate' xs 0 = xs
rotate' [] _ = []
rotate' l@(x:xs) i
  | i > len = rotate l (mod i len)
  | i > 0 = rotate' (xs ++ [x]) (i - 1)
  | i < 0 = rotate' l $ length l + i
  where len = length l
```

Problem 20
----------

### Description

Remove the K'th element from a list.

Example in Haskell:

    Prelude> removeAt 1 "abcd"
    ('b',"acd")

### Solutions
```haskell
removeAt :: Int -> [a] -> (Maybe a, [a])
removeAt _ [] = (Nothing, [])
removeAt 0 (x:xs) = (Just x, xs)
removeAt k (x:xs) = let (a,r) = removeAt (k-1) xs in (a, x:r)
    
removeAt' :: Int -> [a] -> (a, [a])
removeAt' k xs = let (h, t) = splitAt (k + 1) xs in (last h, init h ++ t)
```

Problem 21
----------

### Description

Insert an element at a given position into a list.

Example in Haskell:

    Prelude> insertAt 'X' "abcd" 2
    "aXbcd"
    
### Solutions
```haskell
insertAt :: a -> [a] -> Int -> [a]
insertAt el xs 1 = el:xs
insertAt _ [] _ = []
insertAt el (x:xs) n = x:insertAt el xs (n-1)
    
insertAt' :: a -> [a] -> Int -> [a]
insertAt' el xs n = let (h,t)=splitAt n xs in h ++ el:t
```

Problem 22
----------

### Description

Create a list containing all integers within a given range.

Example in Haskell:

    Prelude> range 4 9
    [4,5,6,7,8,9]
    
### Solutions
```haskell
range :: Int -> Int -> [Int]
range f l 
  | f <= l = [f..l]
  | otherwise = reverse $ range l f
  
range' :: Int -> Int -> [Int]
range' f l
  | f == l = [f]
  | f < l = f:range' (f+1) l
  | f > l = f:range' (f-1) l
```

Problem 23
----------

### Description
Extract a given number of randomly selected elements from a list.

Example in Haskell:

    Prelude> rnd_select "abcdefgh" 3 >>= putStrLn
    eda

### Solutions
```haskell
rnd_select :: [a] -> Int -> IO [a]
rnd_select [] _ = return []
rnd_select xs n
  | n <= 0 = error "n must be greater than zero."
  | otherwise = replicateM n rand
  where rand = do r <- randomRIO (0, (length xs) - 1)
                  return (xs!!r)
```

Problem 24
----------

### Description
Lotto: Draw N different random numbers from the set 1..M.

Example in Haskell:

    Prelude> diff_select 6 49
    [23,1,17,33,21,37]

### Solutions
```haskell
diff_select :: Int -> Int -> IO [Int]
diff_select n m = diff_select' n [1..m]
    
diff_select' :: Int -> [a] -> IO [a]
diff_select' 0 _ = return []
diff_select' _ [] = error "too few elements to choose from"
diff_select' n xs = do r <- randomRIO (0,(length xs) - 1)
                       let remain = take r xs ++ drop (r+1) xs
                       rest <- diff_select' (n-1) remain
                       return ((xs!!r):rest)
```

Problem 25
----------

### Description
Generate a random permutation of the elements of a list.

Example in Haskell:

    Prelude> rnd_permu "abcdef"
    "badcef"

### Solutions
```haskell
rnd_permu :: [a] -> IO [a]
rnd_permu xs = diff_select' (length xs) xs
```

Problem 26
----------

### Description
Generate the combinations of K distinct objects chosen from the N elements of a list.

In how many ways can a committee of 3 be chosen from a group of 12 people? We all know that there are C(12,3) = 220 possibilities (C(N,K) denotes the well-known binomial coefficients). For pure mathematicians, this result may be great. But we want to really generate all the possibilities in a list.

Example in Haskell:

    Prelude> combinations 3 "abcdef"
    ["abc","abd","abe",...]
    
### Solutions
```haskell
combinations :: Int -> [a] -> [[a]]
combinations _ [] = []
combinations 1 xs = map (\x -> [x]) xs
combinations n (x:xs) = combinations n xs ++ (map (x:) $ combinations (n-1) xs)
```

Problem 27
----------

### Description
Group the elements of a set into disjoint subsets.

a) In how many ways can a group of 9 people work in 3 disjoint subgroups of 2, 3 and 4 persons? Write a function that generates all the possibilities and returns them in a list.

b) Generalize the above predicate in a way that we can specify a list of group sizes and the predicate will return a list of groups.

Note that we do not want permutations of the group members; i.e. ((ALDO BEAT) ...) is the same solution as ((BEAT ALDO) ...). However, we make a difference between ((ALDO BEAT) (CARLA DAVID) ...) and ((CARLA DAVID) (ALDO BEAT) ...).

You may find more about this combinatorial problem in a good book on discrete mathematics under the term "multinomial coefficients".

Example in Haskell:

    Prelude> group [2,3,4] ["aldo","beat","carla","david","evi","flip","gary","hugo","ida"]
    [[["aldo","beat"],["carla","david","evi"],["flip","gary","hugo","ida"]],...]
    (altogether 1260 solutions)
    
    Prelude> group [2,2,5] ["aldo","beat","carla","david","evi","flip","gary","hugo","ida"]
    [[["aldo","beat"],["carla","david"],["evi","flip","gary","hugo","ida"]],...]
    (altogether 756 solutions)
    
### Solutions

It's a hard problem at first glance. We need a helper function not only just returns the combination but also returns the rest of the original list. The rest part is useful later. So we define a function called "combinationsMore"

```haskell
combinationsMore :: Int -> [a] -> [([a], [a])]
combinationsMore 0 xs = [([], xs)]
combinationsMore n [] = []
combinationsMore n (x:xs) = l1 ++ l2
  where l1 = [ (x:ys, zs) | (ys, zs) <- combinationsMore (n-1) xs ]
        l2 = [ (ys, x:zs) | (ys, zs) <- combinationsMore n xs]
```

Then we could use list comprehension again to get the final definition:

```haskell
groupDisjoint :: [Int] -> [a] -> [[[a]]]
groupDisjoint [] _ = [[]]
groupDisjoint (n:ns) xs = [ g:gs | (g, rs) <- combinationsMore n xs, gs <- groupDisjoint ns rs]
```
Problem 28
----------

### Description
Sorting a list of lists according to length of sublists.

a) We suppose that a list contains elements that are lists themselves. The objective is to sort the elements of this list according to their length. E.g. short lists first, longer lists later, or vice versa.

Example in Haskell:

    Prelude> lsort ["abc","de","fgh","de","ijkl","mn","o"]
    ["o","de","de","mn","abc","fgh","ijkl"]

b) Again, we suppose that a list contains elements that are lists themselves. But this time the objective is to sort the elements of this list according to their length frequency; i.e., in the default, where sorting is done ascendingly, lists with rare lengths are placed first, others with a more frequent length come later.

Example in Haskell:

    Prelude> lfsort ["abc", "de", "fgh", "de", "ijkl", "mn", "o"]
    ["ijkl","o","abc","fgh","de","de","mn"]
    
### Solutions
```haskell
lsort :: [[a]] -> [[a]]
lsort = sortBy (comparing length)
    
lfsort :: [[a]] -> [[a]]
lfsort = concat . lsort . groupBy ((==) `on` length) . lsort
```

Problem 31
----------

### Description
Determine whether a given integer number is prime.

Example in Haskell:

    Prelude> isPrime 7
    True
    
### Solutions
```haskell
isPrime :: Integral a => a -> Bool
isPrime n 
  | n < 2 = False
  | otherwise = all ((/= 0).mod n) $ 2:[3,5..s]
  where s = floor $ sqrt $ fromIntegral n
```

Problem 32
----------

### Description
Determine the greatest common divisor of two positive integer numbers. Use Euclid's algorithm.

Example in Haskell:

    Prelude> [myGCD 36 63, myGCD (-3) (-6), myGCD (-3) 6]
    [9,3,3]

### Solutions
```haskell
myGCD :: Integer -> Integer -> Integer
myGCD a b 
  | a < 0 = myGCD (-a) b
  | b < 0 = myGCD a (-b)
  | b == 0 = a
  | otherwise = myGCD b (a `mod` b)
```

Problem 33
----------

### Description
Determine whether two positive integer numbers are coprime. Two numbers are coprime if their greatest common divisor equals 1.

Example in Haskell:

    Prelude> coprime 35 64
    True
    
### Solutions
```haskell
coprime :: Integral a => a -> a -> Bool
coprime a b = gcd a b == 1
```

Problem 34
----------

### Description

Calculate Euler's totient function phi(m).

Euler's so-called totient function phi(m) is defined as the number of positive integers r (1 <= r < m) that are coprime to m.

Example: m = 10: r = 1,3,7,9; thus phi(m) = 4. Note the special case: phi(1) = 1.

Example in Haskell:

    Prelude> totient 10
    4
    
### Solutions
```haskell
totient :: Int -> Int
totient x = length $ filter (coprime x) [1..(x-1)]
```

Problem 35
----------

### Description

Determine the prime factors of a given positive integer. Construct a flat list containing the prime factors in ascending order.

Example in Haskell:

    Prelude> primeFactors 315
    [3, 3, 5, 7]

### Solutions
```haskell
primeFactors :: Int -> [Int]
primeFactors x = testDiv x $ 2:[3,5..(floor $ sqrt $ fromIntegral x)]
  where testDiv _ [] = []
        testDiv target (y:ys)
          | target `mod` y == 0 = yys ++ (testDiv targetRemain ys)
          | otherwise = testDiv target ys
          where (yys, targetRemain) = tryRemove target y
                tryRemove m n
                  | m `mod` n /= 0 = ([], m)
                  | otherwise = (n:ns, remainM)
                  where (ns, remainM) = tryRemove (m `div` n) n
```
